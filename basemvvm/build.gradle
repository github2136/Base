plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

def lib = rootProject.ext.dependencies
def version = rootProject.ext.android

android {
    compileSdkVersion version.compileSdkVersion
    defaultConfig {
        minSdkVersion version.minSdkVersion
        targetSdkVersion version.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        dataBinding = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    afterEvaluate {
        publishing {
            publications {
                maven(MavenPublication) {
                    artifact sourceJar // 增加上传源码的 task
                    afterEvaluate { artifact(tasks.getByName("bundleReleaseAar")) } //生成aar包
                    groupId = 'com.github.github2136.Base'
                    artifactId = 'basemvvm'
                    version = '1.0'
                }
            }
        }
    }
    android.libraryVariants.all { var ->
        var.outputs.all { output ->
            outputFileName = "basemvvm-${var.buildType.name}-${defaultConfig.versionName}-${releaseTime()}.aar"
        }
    }
}
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.getSrcDirs() // 源码路径
    archiveClassifier = "sources-${android.defaultConfig.versionName}-${releaseTime()}"
}
artifacts {
    archives sourceJar
}
dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    //Android库
    api lib["android-appcompat"]
    api lib["android-recyclerview"]
    api lib["android-material"]
    api lib["android-swiperefreshlayout"]
    api lib["core-ktx"]
    api lib["android-viewmodel-ktx"]
    api lib["android-lifecycle-ktx"]
    //协程
    api lib["android-coroutines"]
    //gson
    api lib['gson']
    //工具类
    api lib["android-util"]
    //logcat日志打印
    api lib["logger"]
    //okhttp
    api lib["okhttp"]
    api lib["okhttp-retrofit"]
    api lib["okhttp-gson"]

    androidTestImplementation lib["android-test"]
    testImplementation lib["junit"]
    androidTestImplementation lib["espresso"]
}
static def releaseTime() {
    return new Date().format("MMdd", TimeZone.getTimeZone("GMT+08"))
}
